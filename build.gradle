plugins {
  id 'java'
  id 'maven'
  id 'jacoco'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.4'
  id 'org.sonarqube' version '1.2'
}

group='com.github.aesteve'
version='0.1-SNAPSHOT'

repositories {
  jcenter()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

configurations {
  beanValidationTestCompile.extendsFrom testCompile
  beanValidationTestCompile.extendsFrom compileOnly
  beanValidationTestRuntime.extendsFrom testRuntime
}


dependencies {
  compile "io.vertx:vertx-core:$vertxVersion"
  compileOnly 'org.hibernate:hibernate-validator:5.2.4.Final'

  testCompile 'junit:junit:4.12'
  testCompile "io.vertx:vertx-unit:$vertxVersion"

  beanValidationTestRuntime 'javax.el:javax.el-api:2.2.4'
  beanValidationTestRuntime 'org.glassfish.web:javax.el:2.2.4'
}

// add a different source test for bean validation with validator jar in classpath
// so that we can make sure everything's working fine even without validator's jar
sourceSets {
  beanValidationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/beanvalidation-test/java')
    }
    resources.srcDir file('src/beanvalidation-test/resources')
  }
}


bintray {
  user = 'aesteve'
  key = System.getenv 'BINTRAY_KEY'
  pkg {
    repo = 'maven'
    name = project.name
    licenses = ['Apache-2.0']
    vcsUrl = "https://github.com/aesteve/${project.name}"
    labels = ['vertx', 'vert.x', 'configuration', 'typefsafe']
    publicDownloadNumbers = true
    version {
      name = project.version
      description = 'Provides a way to map json config to a typed configuration object'
    }
    publications = ['maven']
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allJava
}

test.outputs.upToDateWhen { false }
test {
  finalizedBy jacocoTestReport
}

tasks.sonarqube.dependsOn = []


task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
